#!/usr/bin/env coffee

###
Simple stripe script to manage customers.

options:
-- method - <string> main function to call
-- getAllDelete
-- dry -  string to boolean to test dry runs
-- methodOpts - json method options
-- filter - to become methodOpts.filter
-- regExp - to become methodOpts.regExp
-- delUsers - delete all related auth_users as well (boolean)
-- api - string: 'live' or 'test' defaults to config

Example:

foreman run ./scripts/stripe/customers --getAllDelete=true --filter=description --regExp="nick\+.*"

  Will delete all accounts with a description matching the regExp nick\+.*

foreman run ./scripts/stripe/customers --getAllDelete=true --filter=description --regExp="nick\+.*" --dry=delete

  Will get all customers matching the regExp but not delete them.

foreman run ./scripts/stripe/customers --method=getAllStream --filter=description --regExp="nick\+.*"

  Will get all customers matching the regExp.
###
Promise = require 'bluebird'
_ = require 'lodash'
Spinner = require('cli-spinner').Spinner
JSON5 = require 'json5'
through = require 'through2'
argv = require('yargs').argv
config = require '../../backend/config/config'
logger = require('../../backend/config/logger').spawn('scripts:stripe:customer')

{method, getAllDelete, dry, methodOpts, filter, regExp, delUsers, api} = argv

if api == 'live'
  logger.info "LIVE MODE"
  config.PAYMENT_PLATFORM.LIVE_MODE = true
  config.ALLOW_LIVE_APIS = true

stripeSvcPromise = require('../../backend/services/payment/stripe')(api)
tables = require '../../backend/config/tables'
require('../../backend/extensions/stream')



makeSpinner = () ->
  spinner = new Spinner('running.. %s')
  spinner.setSpinnerString('|/-\\')
  spinner.start()
  spinner

methodOpts = JSON5.parse(methodOpts || '{}')

if filter? && regExp
  _.assign methodOpts, filter: {key: filter, regExp}

if dry == 'delete'
  dryDelete = true
  dry = false
else
  dry = dry == 'true'

if getAllDelete || method == 'getAllDelete'
  method = 'getAllStream'
  doDelete = true
  if !dry && !dryDelete && delUsers?
    doDelUsers = true

if !method
  console.error("Method argument required")
  process.exit(1)

spin = makeSpinner()

logger.debug -> 'Derrived Options'
logger.debug -> {method, getAllDelete, dry, methodOpts, dryDelete, delUsers}

handleStream = ({stream, stripeSvc}) ->
  return stream
  .pipe through.obj (customer, enc, cb) ->
    if dryDelete || !doDelete
      log = true
      logger.debug ->
        log = false
        "Customer: #{customer.id}"

      logger.debug ->
        "Customer: Filter: #{filter}:#{customer[filter]}"

      if log
        console.log  "Customer: #{customer.id}"
        console.log "Customer: Filter: #{filter}:#{customer[filter]}"

      if dryDelete
        console.log  "DRY DELETION of: #{customer.id}"
      return cb()

    console.log "handleStream: DOING REAL DELETE."

    stripeSvc.customers.remove(customer, false)
    .then () ->
      logger.debug -> "Deleted Customer: #{customer.id}"
      cb()
    .catch (e) ->
      cb(e)
    return
  .toPromise()

handlePromise = ({result, stripeSvc}) ->
  results = if Array.isArray(result.data)
    result.data
  else
    [result.data]

  Promise.each results, (customer) ->
    console.log  "handlePromise: Handling customer: #{customer.id}"

    if !doDelete
      return customer

    if dryDelete
      console.log  "DRY DELETION of: #{customer.id}"
      return customer

    console.log "handlePromise: DOING REAL DELETE."
    stripeSvc.customers.remove(customer)
    .then (res) ->
      logger.debug -> "Deleted Customer: #{customer.id}"
      logger.debug -> "handlePromise: response"
      logger.debug -> res
      customer

stripeSvcPromise
.then (stripeSvc) -> Promise.try ->
  methodToRun = stripeSvc.customers[method]
  if !methodToRun?
    throw new Error('Invalid Method')

  mainPromiseFn = () ->
    if dry
      return Promise.resolve(logger.debug -> "Dry execution with #{JSON5.stringify methodOpts}")

    logger.debug -> "Executing main method: #{method}"
    Promise.resolve(methodToRun(methodOpts))


  mainPromiseFn()
  .then (maybeStream) ->
    logger.debug ->
      logger.debug -> "Done main method."

    if maybeStream.pipe?
      return handleStream({stream: maybeStream, stripeSvc})

    handlePromise({result: maybeStream, stripeSvc})
.then () ->
  if doDelUsers && delUsers && !dry
    logger.debugQuery(tables.auth.user()
    .where('email', 'like', delUsers)
    .del())
.then () ->
  logger.debug -> "@@@@ TOTAL SUCCESS @@@@"
.catch (e) ->
  logger.debug -> "@@@@ TOTAL Failure @@@@"
  logger.error(e.message)
  process.exit(1)
.finally ->
  spin.stop()
  process.exit(0)
